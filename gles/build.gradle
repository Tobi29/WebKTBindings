/*
 * Copyright 2012-2019 Tobi29
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

if (rootProject.enableAndroid) {
    apply plugin: "com.android.library"
}
apply plugin: "kotlin-multiplatform"
apply from: rootProject.file("gradle/publish-maven.gradle")
apply from: rootProject.file("gradle/build-android.gradle")

kotlin {
    sourceSets {
        commonMain {
            dependencies {
                implementation kotlin("stdlib-common")
                api project(":binding-utils")
            }
        }
        commonUnmanagedMain {
            dependsOn commonMain
        }
        commonUnmanagedTest {
            dependsOn commonTest
        }
        jsMain {
            dependencies {
                implementation kotlin("stdlib-js")
            }
        }
        "jvm-lwjgl3Main" {
            dependsOn commonUnmanagedMain
            dependencies {
                implementation kotlin("stdlib")
                implementation rootProject.lwjgl("lwjgl")
                implementation rootProject.lwjgl("lwjgl-egl")
                implementation rootProject.lwjgl("lwjgl-opengles")
            }
        }
        "jvm-lwjgl3Test" {
            dependsOn commonUnmanagedTest
        }
    }
    targets {
        // Kotlin/JS
        fromPreset(presets.js, "js")

        // Kotlin/JVM (LWJGL 3)
        fromPreset(presets.jvm, "jvm-lwjgl3")
    }
}

if (rootProject.enableAndroid) {
    android {
        defaultConfig {
            minSdkVersion 18
        }
    }

    kotlin {
        sourceSets {
            androidMain {
                dependsOn commonUnmanagedMain
                dependencies {
                    implementation kotlin("stdlib")
                }
            }
            androidTest {
                dependsOn commonUnmanagedTest
            }
        }
        targets {
            // Kotlin/Android
            fromPreset(presets.android, "android")
        }
        android {
            publishLibraryVariants("release", "debug")
        }
    }
}
