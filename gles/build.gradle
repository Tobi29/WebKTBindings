/*
 * Copyright 2012-2018 Tobi29
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

apply plugin: "com.android.library"
apply plugin: "kotlin-multiplatform"
apply from: rootProject.file("gradle/publish-maven.gradle")

android {
    compileSdkVersion = 28

    defaultConfig {
        targetSdkVersion 28
        minSdkVersion 18
        versionCode = 1
        versionName = version
    }

    sourceSets {
        main.java.srcDirs += "src/androidMain/kotlin"
    }
}

kotlin {
    sourceSets {
        commonMain {
            dependencies {
                implementation rootProject.kotlin("kotlin-stdlib")
                api project(":binding-utils")
            }
        }
        commonUnmanagedMain {
            dependsOn commonMain
        }
        commonUnmanagedTest {
            dependsOn commonTest
        }
        jsMain {
            dependencies {
                implementation rootProject.kotlinJS("kotlin-stdlib")
            }
        }
        "jvm-lwjgl3Main" {
            dependsOn commonUnmanagedMain
            dependencies {
                implementation rootProject.kotlinJVM("kotlin-stdlib")
                implementation rootProject.lwjglJVM("lwjgl")
                implementation rootProject.lwjglJVM("lwjgl-opengles")
            }
        }
        "jvm-lwjgl3Test" {
            dependsOn commonUnmanagedTest
        }
        androidMain {
            dependsOn commonUnmanagedMain
            dependencies {
                implementation rootProject.kotlinJVM("kotlin-stdlib")
            }
        }
        androidTest {
            dependsOn commonUnmanagedTest
        }
    }
    targets {
        // Kotlin/JS
        fromPreset(presets.js, "js")

        // Kotlin/JVM (LWJGL 3)
        fromPreset(presets.jvm, "jvm-lwjgl3")

        // Kotlin/Android
        fromPreset(presets.android, "android")
    }
}

mavenAarDependencies += [[groupId: "net.gitout.kt-bindings",
        artifactId: "binding-utils-jvm",
        version: version,
        scope: "compile"],
        [groupId: "net.gitout.kt-bindings",
                artifactId: "gles-metadata",
                version: version,
                scope: "compile"],
        [groupId: "org.jetbrains.kotlin",
                artifactId: "kotlin-stdlib",
                version: rootProject.ext.kotlinVersion,
                scope: "runtime"],
        [groupId: "org.jetbrains.kotlin",
                artifactId: "kotlin-stdlib-common",
                version: rootProject.ext.kotlinVersion,
                scope: "runtime"]]

kotlin {
    sourceSets.each { sourceSet ->
        sourceSet.languageSettings {
            useExperimentalAnnotation "kotlin.ExperimentalUnsignedTypes"
        }
    }
}
