/*
 * Copyright 2012-2019 Tobi29
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

apply plugin: "com.android.library"
apply plugin: "kotlin-multiplatform"
apply from: rootProject.file("gradle/publish-maven.gradle")

android {
    compileSdkVersion = 28

    defaultConfig {
        targetSdkVersion 28
        minSdkVersion 18
        versionCode = 1
        versionName = version

        externalNativeBuild {
            cmake {
                arguments "-DALSOFT_ENABLE_JNI=ON",
                        "-DALSOFT_UTILS=OFF",
                        "-DALSOFT_EXAMPLES=OFF",
                        "-DALSOFT_TESTS=OFF",
                        "-DALSOFT_INSTALL=OFF"
            }
        }
    }

    sourceSets {
        main.java.srcDirs += "src/androidMain/kotlin"
    }

    externalNativeBuild {
        cmake {
            path "ext/openal-soft/CMakeLists.txt"
        }
    }
}

kotlin {
    sourceSets {
        commonMain {
            dependencies {
                implementation kotlin("stdlib-common")
                api project(":binding-utils")
            }
        }
        "jvm-lwjgl3Main" {
            dependencies {
                implementation kotlin("stdlib")
                implementation rootProject.lwjgl("lwjgl")
                implementation rootProject.lwjgl("lwjgl-openal")
            }
        }
        androidMain {
            dependencies {
                implementation kotlin("stdlib")
            }
        }
        nativeMain {
            dependsOn commonMain
        }
        nativeTest {
            dependsOn commonTest
        }
        configure([nativeLinuxX64Main, nativeLinuxArm32HfpMain,
                nativeLinuxMips32Main, nativeLinuxMipsel32Main]) {
            dependsOn nativeMain
        }
        configure([nativeLinuxX64Test, nativeLinuxArm32HfpTest,
                nativeLinuxMips32Test, nativeLinuxMipsel32Test]) {
            dependsOn nativeTest
        }
    }
    targets {
        // Kotlin/JVM (LWJGL 3)
        fromPreset(presets.jvm, "jvm-lwjgl3")

        // Kotlin/Android
        fromPreset(presets.android, "android")

        // Kotlin/Native
        fromPreset(presets.linuxX64, "nativeLinuxX64")
        fromPreset(presets.linuxArm32Hfp, "nativeLinuxArm32Hfp")
        fromPreset(presets.linuxMips32, "nativeLinuxMips32")
        fromPreset(presets.linuxMipsel32, "nativeLinuxMipsel32")
        configure([nativeLinuxX64, nativeLinuxArm32Hfp,
                nativeLinuxMips32, nativeLinuxMipsel32]) {
            compilations.main.cinterops {
                al {
                    packageName "binding.al"
                    includeDirs file("ext/openal-soft/include")
                }
            }
        }
    }
    android {
        publishLibraryVariants("release", "debug")
    }
}
