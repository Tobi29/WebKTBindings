/*
 * Copyright 2012-2019 Tobi29
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

apply plugin: "kotlin-multiplatform"
apply from: rootProject.file("gradle/publish-maven.gradle")

kotlin {
    sourceSets {
        commonMain {
            dependencies {
                implementation rootProject.kotlin("kotlin-stdlib")
                api project(":binding-utils")
            }
        }
        "jvm-lwjgl3Main" {
            dependencies {
                implementation rootProject.kotlinJVM("kotlin-stdlib")
                implementation rootProject.lwjglJVM("lwjgl")
                implementation rootProject.lwjglJVM("lwjgl-tinyfd")
            }
        }
        nativeMain {
            dependsOn commonMain
        }
        nativeTest {
            dependsOn commonTest
        }
        configure([nativeLinuxX64Main, nativeLinuxArm32HfpMain]) {
            dependsOn nativeMain
        }
        configure([nativeLinuxX64Test, nativeLinuxArm32HfpTest]) {
            dependsOn nativeTest
        }
    }
    targets {
        // Kotlin/JVM (LWJGL 3)
        fromPreset(presets.jvm, "jvm-lwjgl3")

        // Kotlin/Native
        fromPreset(presets.linuxX64, "nativeLinuxX64")
        fromPreset(presets.linuxArm32Hfp, "nativeLinuxArm32Hfp")
        configure([nativeLinuxX64, nativeLinuxArm32Hfp]) {
            compilations.main.cinterops {
                tinyfd {
                    packageName "binding.tinyfd"
                    includeDirs file("ext")
                    includeDirs file("/usr/include")
                }
            }
        }
    }
}
